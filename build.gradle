plugins {
  id 'application'
  id 'idea'
  id 'jacoco'
  id 'com.github.ben-manes.versions' version '0.53.0'
  id 'com.github.hmcts.rse-cft-lib' version '0.19.2012'
  id 'com.github.spacialcircumstances.gradle-cucumber-reporting' version '0.1.25'
  id 'io.freefair.lombok' version '8.14.4'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.owasp.dependencycheck' version '12.1.5'
  id 'org.sonarqube' version '6.3.1.5724'
  id 'org.springframework.boot' version '3.5.11'
  id 'uk.gov.hmcts.java' version '0.12.67'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

ext {
  set('springCloudVersion', '2025.0.1')
  set('spring-framework.version', '6.2.14')
  set('spring-security.version', '6.5.7')
  set('snakeyaml.version', '2.3')
  set('jackson.version', '2.17.0')
  appInsightsVersion  = '2.6.4'
  jjwt               = '0.13.0'
  junit               = '5.14.3'
  junitPlatform       = '1.14.3'
  lombok              = '1.18.36'
  testcontainers      = '1.21.4'

}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${junit}",
    "org.junit.jupiter:junit-jupiter-engine:${junit}",
    "org.junit.jupiter:junit-jupiter-params:${junit}",
    "org.junit.platform:junit-platform-launcher:${junitPlatform}",
    "org.junit.platform:junit-platform-commons:${junitPlatform}",
    "org.junit.platform:junit-platform-engine:${junitPlatform}",
    "org.junit.platform:junit-platform-launcher:${junitPlatform}",
    "org.apiguardian:apiguardian-api:1.1.2"
  ]
]

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }
}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  cftlibRuntimeClasspath.extendsFrom(runtimeClasspath)
}

idea {
  module {
    // config to allow Intellij to mark test source and resource files correctly to help linting tools
    testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
    testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
    testResourceDirs += project.sourceSets.functionalTest.resources.srcDirs
    testResourceDirs += project.sourceSets.integrationTest.resources.srcDirs
  }
}

tasks.withType(JavaCompile) {
  // options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

checkstyle {
  toolVersion '13.2.0'
}

tasks.withType(Checkstyle) {
  exclude '**/package-info.java'
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  generateCucumberReports.enabled = true
  failFast = true
}

task smoke(type: JavaExec) {
  description = "Runs smoke tests"
  group = "Verification"

  generateCucumberReports.enabled = false
  mainClass = "uk.gov.hmcts.reform.translate.TranslationServiceBeftaMain"
  classpath += sourceSets.functionalTest.runtimeClasspath + sourceSets.main.output + sourceSets.test.output
  args = [
    '--threads', '10',
    '--plugin', "json:${rootDir}/target/cucumber.json",
    '--plugin', "junit:${buildDir}/test-results/smoke/cucumber.xml",
    '--tags', '@Smoke and not @Ignore',
    '--glue', 'uk.gov.hmcts.befta.player', 'src/functionalTest/resources/features'
  ]
  // '--add-opens=...' added to suppress 'WARNING: An illegal reflective access operation has occurred' in uk.gov.hmcts.befta.util.CucumberStepAnnotationUtils
  jvmArgs = [ '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED' ]

  finalizedBy {
    generateCucumberReports.enabled = true
    generateCucumberReports {
      doLast{
        delete "${rootDir}/BEFTA Report for Smoke Tests/"
        new File("${rootDir}/BEFTA Report for Smoke Tests").mkdirs()
        file("${rootDir}/target/cucumber/cucumber-html-reports").renameTo(file("${rootDir}/BEFTA Report for Smoke Tests"))

        logger.quiet("Smoke test report moved to ---> file://${rootDir}/BEFTA%20Report%20for%20Smoke%20Tests/overview-features.html")
      }
    }
  }

  outputs.upToDateWhen { false }
}

task functional(type: JavaExec) {
  description = "Runs functional tests"
  group = "Verification"

  generateCucumberReports.enabled = false
  mainClass = "uk.gov.hmcts.reform.translate.TranslationServiceBeftaMain"
  classpath += sourceSets.functionalTest.runtimeClasspath + sourceSets.main.output + sourceSets.test.output
  args = [
    '--threads', '10',
    '--plugin', "json:${rootDir}/target/cucumber.json",
    '--plugin', "junit:${buildDir}/test-results/functional/cucumber.xml",
    '--tags', 'not @Ignore',
    '--glue', 'uk.gov.hmcts.befta.player', 'src/functionalTest/resources/features'
  ]
  // '--add-opens=...' added to suppress 'WARNING: An illegal reflective access operation has occurred' in uk.gov.hmcts.befta.util.CucumberStepAnnotationUtils
  jvmArgs = [ '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED' ]

  finalizedBy {
    generateCucumberReports.enabled = true
    generateCucumberReports {
      doLast{
        delete "${rootDir}/BEFTA Report for Functional Tests/"
        new File("${rootDir}/BEFTA Report for Functional Tests").mkdirs()
        file("${rootDir}/target/cucumber/cucumber-html-reports").renameTo(file("${rootDir}/BEFTA Report for Functional Tests"))
        logger.quiet("Functional test report moved to ---> file://${rootDir}/BEFTA%20Report%20for%20Functional%20Tests/overview-features.html")
      }
    }
  }

  outputs.upToDateWhen { false }
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

cucumberReports {
  outputDir = file("${rootDir}/target/cucumber")
  reports = files("${rootDir}/target/cucumber.json")
}

task customCleanBeftaReports(type:Delete) {
  delete "${rootDir}/BEFTA Report for Smoke Tests/"
  delete "${rootDir}/BEFTA Report for Functional Tests/"
  delete "${rootDir}/target/cucumber"
  delete "${rootDir}/target/cucumber.json"
}
tasks.clean.dependsOn(tasks.customCleanBeftaReports)

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

dependencies {

  // start::CVE Vulnerability dependency overrides                                                            // MAIN PARENT DEPENDEDNCY

  // PUT CVE Vulnerability dependency overrides here
  // e.g. implementation group: 'com.example', name: 'example-lib', version: '1.2.3'

  // end::CVE Vulnerability dependency overrides                                                              // MAIN PARENT DEPENDEDNCY



  // SPRING
  implementation group: 'org.springframework.boot', name: 'spring-boot'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-client'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-resource-server'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-config-server'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-config'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-config-client'
  implementation group: 'org.springframework.security', name: 'spring-security-web'
  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose'
  implementation group: 'org.springframework.security', name: 'spring-security-config'
  implementation group: 'org.springframework.retry', name:'spring-retry'
  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.8.15'

  // HMCTS
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.3.3'
  implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '3.0.5'
  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.1.9'


  implementation group: 'com.auth0', name: 'java-jwt', version: '4.5.1'
  implementation group: 'com.microsoft.azure', name: 'applicationinsights-logging-logback', version: appInsightsVersion
  implementation group: 'com.microsoft.azure', name: 'applicationinsights-spring-boot-starter', version: appInsightsVersion
  implementation group: 'org.flywaydb', name: 'flyway-core', version: '9.22.3'
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.10'

  testImplementation libraries.junit5
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testImplementation group: 'org.assertj', name: 'assertj-vavr', version: '0.4.3'

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath
  integrationTestImplementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.2.3'
  integrationTestImplementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: jjwt
  integrationTestRuntimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: jjwt
  integrationTestRuntimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: jjwt
  integrationTestImplementation group: 'com.pivovarit', name: 'throwing-function', version: '1.6.1'
  integrationTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock'
  integrationTestImplementation group: 'org.testcontainers', name: 'postgresql', version: testcontainers

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath
  functionalTestImplementation group: 'io.rest-assured', name: 'rest-assured', version: '5.5.6'
  functionalTestImplementation group: 'com.github.hmcts', name: 'befta-fw', version: '9.2.4'
  functionalTestImplementation group: 'com.github.hmcts', name: 'ccd-test-definitions', version: '7.25.4'

  cftlibImplementation group: 'org.springframework.boot', name: 'spring-boot-devtools'

}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport
project.tasks['check'].dependsOn integration

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: ts-translation-service"
    property "sonar.projectKey", "uk.gov.hmcts.reform:ts-translation-service"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.outputLocation}"
    property "sonar.exclusions",
      "src/main/java/uk/gov/hmcts/reform/translate/Application.java," +
        "src/main/java/uk/gov/hmcts/reform/translate/config/SecurityConfiguration.java"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

apply from: './gradle/suppress.gradle'
// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    name = "AzureArtifacts"
    url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
  }
}

application {
  mainClass = 'uk.gov.hmcts.reform.translate.Application'
}

bootJar {
  archiveFileName = "ts-translation-service.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

bootRun {
  mainClass = 'uk.gov.hmcts.reform.translate.Application'
}

bootWithCCD {

  authMode = uk.gov.hmcts.rse.AuthMode.Local

  environment("RSE_LIB_ADDITIONAL_DATABASES", "ts_translation_service");
  environment("TS_TRANSLATION_SERVICE_DB_USERNAME", "postgres")
  environment("TS_TRANSLATION_SERVICE_DB_PASSWORD", "postgres")
  environment("TS_TRANSLATION_SERVICE_DB_PORT", "6432")
  environment("TS_TRANSLATION_SERVICE_DB_NAME", "ts_translation_service")
  environment("RSE_LIB_S2S_PORT", 4502)
}
